import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id('org.springframework.boot').version('2.6.7')
    id('com.diffplug.spotless').version('6.5.0')
    id('org.openapi.generator').version('5.4.0')
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web:2.7.1')
    implementation('org.springframework.boot:spring-boot-starter-actuator:2.7.1')
    implementation('org.springframework.boot:spring-boot-starter-security:2.7.1')
    implementation('org.springframework:spring-tx:5.3.21')
    // FIXME remove below dependencies
    // https://github.com/OpenAPITools/openapi-generator/issues/8719#issuecomment-806049990
    implementation('org.springdoc:springdoc-openapi-ui:1.6.9')
    implementation('com.amazonaws:aws-java-sdk-cognitoidp:1.12.257')
    implementation('com.auth0:java-jwt:4.0.0')
    implementation('com.auth0:jwks-rsa:0.21.1')
    implementation(project(':common'))
    implementation(project(':database'))
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "${buildDir}/openapi/gen-src/main/java"]
        }
    }
}

spotless {
    encoding('UTF-8')
    java {
        targetExclude('build/openapi/gen-src/main/java/**')
        indentWithSpaces()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat()
    }
}

// see. https://openapi-generator.tech/docs/plugins#gradle
task oaGenerate(type: GenerateTask) {
    generatorName = 'spring'
    inputSpec = "${projectDir}/openapi/index.yml"
    outputDir = "${buildDir}/openapi"
    typeMappings = [
        "string+local-date": "LocalDate",
        "string+local-datetime" : "LocalDateTime"
    ]
    importMappings = [
        "LocalDate": "java.time.LocalDate",
        "LocalDateTime": "java.time.LocalDateTime"
    ]
    configOptions = [
        "hideGenerationTimestamp": "true",
        "sourceFolder": "gen-src/main/java",
        "basePackage": "aldra.api",
        "apiPackage": "aldra.api.adapter.web.controller",
        "modelPackage": "aldra.api.adapter.web.dto",
        "skipDefaultInterface": "true",
        "oas3": "true",
        "dateLibrary": "java8",
        "delegatePattern": "false",
        "interfaceOnly": "true",
        "openApiNullable": "false",
        "useTags": "true",
        "disallowAdditionalPropertiesIfNotPresent": "false",
        "useBeanValidation": "false",
    ]
}

bootRun {
    doFirst {
        file("${rootDir}/.envrc").readLines().each {
            def (key, value) = it.split(" ")[1].tokenize("=")
            environment(key, value)
        }
    }
}
